

uniform mat4 trans_clip_of_mainCam_to_mainRender;


const float ndcNear = CAMERA_NEAR;
const float ndcFar = CAMERA_FAR;
const float ndcA = ndcNear + ndcFar;
const float ndcB = ndcNear - ndcFar;
const float ndcC = 2.0 * ndcNear * ndcFar;
const float ndcD = ndcFar - ndcNear;

float getZFromNdc(vec3 ndcPos) {
  float d = ndcPos.z * ndcB;
  return (ndcC / (ndcA+d));
}

float getZFromNdc(vec3 ndcPos, float near, float far) {
  float d = ndcPos.z * (near-far);
  return ( (2.0 * near * far) / ((near + far) + d));
}

// z has to be in range 0..MAIN_CAMERA_FAR
float getZFromLinearZ(float z) {

    //((((1.0 / (z / ndcC)) - ndcA) / ndcB) / 2.0) + 0.5;

  z /= ndcC;
  z = 1.0 / z;
  z -= ndcA;
  z /= ndcB;
  z /= 2.0;
  z += 0.5;
  return z;
}

float getLinearZFromZ(float z) {
    float z_n = z * 2.0 - 1.0;
    float z_e = ndcC / (ndcA - z_n * ndcD);
    return z_e;
}

float getCustomLinearZFromZ(float z, float near, float far) {
    float z_n = z * 2.0 - 1.0;
    float z_e = 2.0 * near * far / (far + near - z_n * (far - near));
    return z_e;
}

float normalizeZ(float z, float near, float far) {
  return getCustomLinearZFromZ(z, near, far) / far;
}




vec3 calculateSurfacePos(float z, vec2 tcoord, mat4 clipToRender) {
  vec3 ndcPos = vec3(tcoord.xy, z)*2.0 - 1.0;    
  vec4 clipPos = vec4(0);
  clipPos.w = getZFromNdc(ndcPos);
  clipPos.xyz = ndcPos * clipPos.w;
  vec3 surfacePosition = (clipToRender * clipPos).xyz;
  return surfacePosition;
}


vec3 calculateSurfacePos(float z, vec2 tcoord) {
  return calculateSurfacePos(z, tcoord, trans_clip_of_mainCam_to_mainRender);
}

vec3 calculateSurfacePos(float z, vec2 tcoord, float near, float far, mat4 clipToRender) {
  vec3 ndcPos = vec3(tcoord.xy, z)*2.0 - 1.0;    
  vec4 clipPos = vec4(0);
  clipPos.w = getZFromNdc(ndcPos, near, far);
  clipPos.xyz = ndcPos * clipPos.w;
  vec3 surfacePosition = (clipToRender * clipPos).xyz;
  return surfacePosition;
}
