#version 420

#include "Includes/Configuration.include"
#include "Includes/PositionReconstruction.include"

in vec2 texcoord;

uniform sampler2D currentComputation;

layout(rgba16f) uniform image2D lastFrame;
layout(rgba16f) uniform image2D lastPosition;

uniform sampler2D positionBuffer;
uniform sampler2D velocityBuffer;
uniform sampler2D depthTex;


#ifdef USE_DOF
    layout(r16f) writeonly uniform image2D dofStorage;
#endif

uniform int temporalProjXOffs;

uniform mat4 lastMVP;
uniform mat4 currentMVP;
uniform vec3 cameraPosition;

out vec4 result;

const float rejectionFactor = 700.0;
const float texcoordRejectionFactor = 1.0;

void main() {

    // Fetch screen size, so we don't have to pass it as a shader input
    ivec2 screenSize = textureSize(positionBuffer, 0);

    // Screen coordinate (as int vec)
    ivec2 screenCoord = ivec2(texcoord * vec2(screenSize.x, screenSize.y));

    // Screen coordinate (half resolution only)
    ivec2 computationCoord = ivec2( screenCoord.x/2, screenCoord.y);

    #ifdef USE_DOF
        // Compute dof factors
        float bufferZ = texture(depthTex, texcoord).x;
        float linearZ = getLinearZFromZ(bufferZ);
        float dofFactor = (linearZ-4.0)/1.0;
        dofFactor = 0.0;

        // Store dof in the storage
        imageStore(dofStorage, screenCoord, vec4(saturate(dofFactor)) );
    #endif

    vec3 position = texelFetch(positionBuffer, screenCoord, 0).rgb;

    // The last computed value (half resolution only, but up-to-date)
    vec4 lastComputedValue = texelFetch(currentComputation, computationCoord, 0);
    vec2 velocity = -texture(velocityBuffer, texcoord).rg / 255.0;

    vec4 lastProj = lastMVP * vec4(position, 1);
    vec2 lastTexcoord = (lastProj.xy / lastProj.w) * 0.5 + 0.5;
    vec2 newVelocity = (texcoord - lastTexcoord);

    vec2 lastFrameTexcoord = texcoord - velocity;
    ivec2 lastFrameTexcoordInt = ivec2(lastFrameTexcoord * (screenSize)) ;
    ivec2 lastFrameTexcoordInt2 = ivec2((lastTexcoord * (screenSize))) ;
    vec4 oldProjectedColor = imageLoad(lastFrame, lastFrameTexcoordInt);
    vec3 oldProjectedPos = imageLoad(lastPosition, lastFrameTexcoordInt).rgb;
    
    float distanceToCamera = distance(cameraPosition, position);
    float reliability = 1.0 - saturate( (distance(oldProjectedPos, position) / distanceToCamera) * rejectionFactor);

    reliability *= 1.0 - saturate(distance(lastFrameTexcoord, texcoord) * texcoordRejectionFactor);

    result = mix(lastComputedValue, oldProjectedColor, saturate(reliability));

    if ( (screenCoord.x+screenCoord.y) % 2 == 1-temporalProjXOffs) {
        result = lastComputedValue;
    } 
}